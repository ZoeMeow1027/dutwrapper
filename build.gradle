plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'io.dutwrapper'
version '1.11.1-patch1'

repositories {
    mavenCentral()
}

// https://stackoverflow.com/a/34717160
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
// compileJava.options.javaModuleVersion = provider { project.version }

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    // commons-math3 - https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    // Guava - https://mvnrepository.com/artifact/com.google.guava/guava
    implementation 'com.google.guava:guava:33.2.1-jre'

    // JSoup - https://mvnrepository.com/artifact/org.jsoup/jsoup
    implementation 'org.jsoup:jsoup:1.18.1'

    // OkHttp3 - https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'

    // Google Gson - https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.11.0'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dutwrapper/dutwrapper-java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GitHub_Username")
                password = project.findProperty("gpr.key") ?: System.getenv("GitHub_TokenPkg")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}